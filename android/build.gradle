import groovy.json.JsonSlurper

buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

boolean dash = false, hls = false, smoothstreaming = false;

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
}


ext.sdkVersion = 27
ext.buildToolsVersion = '27.0.3'
ext.supportVersion = '27.1.1'

android {
    compileSdkVersion sdkVersion
    buildToolsVersion buildToolsVersion

    defaultConfig {
        minSdkVersion 16 // RN's minimum version
        targetSdkVersion sdkVersion
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    jcenter()
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        url "../node_modules/react-native/android"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation('com.facebook.react:react-native:+') {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }

    // ExoPlayer Core
    implementation 'com.google.android.exoplayer:exoplayer-core:2.7.1'

    // ExoPlayer DASH
    if(dash) {
        implementation 'com.google.android.exoplayer:exoplayer-dash:2.7.1'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-dash:2.7.1'
    }

    // ExoPlayer HLS
    if(hls) {
        implementation 'com.google.android.exoplayer:exoplayer-hls:2.7.1'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-hls:2.7.1'
    }

    // ExoPlayer SmoothStreaming
    if(smoothstreaming) {
        implementation 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.7.1'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-smoothstreaming:2.7.1'
    }

    implementation "com.android.support:support-compat:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:support-media-compat:$supportVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
