import groovy.json.JsonSlurper

buildscript {
    ext.kotlin_version = '1.2.31'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

boolean exoplayer = false, cache = false, auto = false;
boolean dash = false, hls = false, smoothstreaming = false;

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    cache = json.cache ?: cache
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
    auto = json.auto ?: auto

    exoplayer = json.exoplayer || dash || hls || smoothstreaming
}


ext.buildToolsVersion = '23.0.3'
ext.supportVersion = '23.2.0'

android {
    compileSdkVersion 23
    buildToolsVersion buildToolsVersion

    defaultConfig {
        minSdkVersion 16 // RN's minimum version
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }

        consumerProguardFiles 'proguard-rules.txt'
    }

    buildTypes {
        buildTypes.each {
            def autoMd = auto ? 'com.google.android.gms.car.application' : 'disabled.car.application'
            it.manifestPlaceholders = [autoMetadata: autoMd]
        }
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        url "../node_modules/react-native/android"
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'

    // ExoPlayer Core
    if(exoplayer) {
        implementation 'com.google.android.exoplayer:exoplayer-core:r2.4.3'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-core:r2.4.3'
    }

    // ExoPlayer DASH
    if(dash) {
        implementation 'com.google.android.exoplayer:exoplayer-dash:r2.4.3'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-dash:r2.4.3'
    }

    // ExoPlayer HLS
    if(hls) {
        implementation 'com.google.android.exoplayer:exoplayer-hls:r2.4.3'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-hls:r2.4.3'
    }

    // ExoPlayer SmoothStreaming
    if(smoothstreaming) {
        implementation 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.3'
    } else {
        compileOnly 'com.google.android.exoplayer:exoplayer-smoothstreaming:r2.4.3'
    }

    // Caching (for the default Android player)
    if(cache && !exoplayer) {
        implementation 'com.danikula:videocache:2.7.0'
    } else {
        compileOnly 'com.danikula:videocache:2.7.0'
    }

    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
